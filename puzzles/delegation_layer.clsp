; delegation_layer.clsp
; for DataLayer.storage by yakuhito

;; Allows the owner of a store to delegate a fixed set of capabilities to
;; other puzzle hashes.
;; This layer will only re-create itself if the inner puzzle did not emit any
;; odd-amount CREATE_COIN condition. 
;; Capabilities are restricted via filters.

(mod (
    MOD_HASH
    INNER_PUZZLE_HASH
    MERKLE_ROOT
    merkle_proof ; () if running inner puzzle
    puzzle_reveal
    puzzle_solution
  )

  (include condition_codes.clib)
  (include merkle_utils.clib)
  (include sha256tree.clib)
  (include curry.clib)

  (defconstant NEW_MERKLE_ROOT_CONDITION -13)

  ; only one odd CREATE_COIN enforced in upper layers
  (defun morph_conditions (MOD_HASH INNER_PUZZLE_HASH conditions new_merkle_root_info odd_create_coin_found)
    (if conditions
      ; todo: I don't understand why my initial code also checked (= (strlen (f (r (f conditions)))) 32)
      ;; will have to come back, but, for now, removing the condition
      (if (= (f (f conditions)) NEW_MERKLE_ROOT_CONDITION)
        (morph_conditions
          MOD_HASH INNER_PUZZLE_HASH
          (r conditions)
          (r (f conditions))
          odd_create_coin_found
        )
        ; else
        (c
          (f conditions)
          (morph_conditions
            MOD_HASH INNER_PUZZLE_HASH
            (r conditions)
            new_merkle_root_info
            (any
              odd_create_coin_found
              (if (= (f (f conditions)) CREATE_COIN) (= (logand (f (r (r (f conditions)))) 1) 1) ())
            )
          )
        )
      )
      ; else
      (if odd_create_coin_found
        ()
        ; else
        (list
          (list
            CREATE_COIN
            (curry_hashes_inline MOD_HASH
              (sha256 1 MOD_HASH)
              (sha256 1 INNER_PUZZLE_HASH)
              (sha256 1 (f new_merkle_root_info))
            )
            1
            (r new_merkle_root_info)
          )
        )
      )
    )
  )

  ; will return () if all's ok, raise if not
  (defun verify_puzzle_hash (puzzle_hash INNER_PUZZLE_HASH MERKLE_ROOT merkle_proof)
    (if (= puzzle_hash INNER_PUZZLE_HASH)
      ()
      ; else
      (if (= MERKLE_ROOT (simplify_merkle_proof puzzle_hash merkle_proof)) () (x "invalid ph")) 
    )
  )

  (morph_conditions
    MOD_HASH
    INNER_PUZZLE_HASH
    (a puzzle_reveal puzzle_solution)
    (list MERKLE_ROOT)
    (verify_puzzle_hash (sha256tree puzzle_reveal) INNER_PUZZLE_HASH MERKLE_ROOT merkle_proof) ; ()
  )
)
