; one_of_many.clsp
; for DataLayer.storage by yakuhito

;; Allows running one inner puzzle out of a set of multiple possibilities.
;; This layer will only re-create itself if the inner puzzle did not emit any
;; odd-amount CREATE_COIN condition. 

(mod (
    MOD_HASH
    MERKLE_ROOT
    puzzle_reveal
    merkle_proof
    puzzle_solution
  )

  (include condition_codes.clib)
  (include merkle_utils.clib)
  (include sha256tree.clib)
  (include curry.clib)

  ; only one odd CREATE_COIN enforced in upper layers
  (defun morph_conditions (MOD_HASH MERKLE_ROOT conditions odd_create_coin_found)
    (if conditions
      (c
        (f conditions)
        (morph_conditions
          MOD_HASH MERKLE_ROOT
          (r conditions)
          (any
            odd_create_coin_found
            (all (= (f (f conditions)) CREATE_COIN) (= (logand (f (r (r (f conditions)))) 1) 1))
          )
        )
      )
      ; else
      (if odd_create_coin_found
        ()
        ; else
        (list
          (list
            CREATE_COIN
            (curry_hashes_inline MOD_HASH (sha256 1 MOD_HASH) (sha256 1 MERKLE_ROOT))
            1 ; NFT1 amount is always 1
          )
        )
      )
    )
  )

  (if (= MERKLE_ROOT (simplify_merkle_proof (sha256tree puzzle_reveal) merkle_proof))
    (morph_conditions MOD_HASH MERKLE_ROOT (a puzzle_reveal puzzle_solution) ())
    ; else
    (x "ph proof invalid")
  )
)
