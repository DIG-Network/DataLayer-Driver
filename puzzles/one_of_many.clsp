; one_of_many.clsp
; for DataLayer.storage by yakuhito

;; Allows running one inner puzzle out of a set of multiple possibilities.
;; This layer will only re-create itself if the inner puzzle did not emit any
;; odd-amount CREATE_COIN condition. 

(mod (
    MOD_HASH
    MERKLE_ROOT
    puzzle_reveal
    merkle_proof
    puzzle_solution
  )

  (include condition_codes.clib)
  (include merkle_utils.clib)
  (include sha256tree.clib)
  (include curry.clib)

  (defconstant NEW_MERKLE_ROOT_CONDITION -13)

  ; only one odd CREATE_COIN enforced in upper layers
  (defun morph_conditions (MOD_HASH conditions new_merkle_root odd_create_coin_found)
    (if conditions
      (if (all (= (f (f conditions)) NEW_MERKLE_ROOT_CONDITION) (= (strlen (f (r (f conditions)))) 32))
        (morph_conditions
          MOD_HASH
          (r conditions)
          (f (r (f conditions)))
          odd_create_coin_found
        )
        ; else
        (c
          (f conditions)
          (morph_conditions
            MOD_HASH
            (r conditions)
            new_merkle_root
            (any
              odd_create_coin_found
              (all (= (f (f conditions)) CREATE_COIN) (= (logand (f (r (r (f conditions)))) 1) 1))
            )
          )
        )
      )
      ; else
      (if odd_create_coin_found
        ()
        ; else
        (list
          (list
            CREATE_COIN
            (curry_hashes_inline MOD_HASH
              (sha256 1 MOD_HASH)
              (sha256 1 new_merkle_root)
            )
            1
          )
        )
      )
    )
  )

  (if (= MERKLE_ROOT (simplify_merkle_proof (sha256tree puzzle_reveal) merkle_proof))
    (morph_conditions MOD_HASH (a puzzle_reveal puzzle_solution) MERKLE_ROOT ())
    ; else
    (x "ph proof invalid")
  )
)
