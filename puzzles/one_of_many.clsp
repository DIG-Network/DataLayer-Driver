; one_of_many.clsp
; for DataLayer.storage by yakuhito

;; Allows running one inner puzzle out of a set of multiple possibilities.
;; This layer will only re-create itself if the inner puzzle did not emit any
;; odd-amount CREATE_COIN condition. 

(mod (
    MOD_HASH
    MERKLE_ROOT
    puzzle_reveal
    merkle_proof
    puzzle_solution
  )

  (include condition_codes.clib)
  (include merkle_utils.clib)
  (include sha256tree.clib)
  (include curry.clib)

  (defun should_recreate (conditions)
    (if conditions
      (if (all (= (f (f conditions)) CREATE_COIN) (= (logand (f (r (r (f conditions)))) 1) 1))
        ()
        ; else
        (should_recreate (r conditions))
      )
      ; else
      1
    )
  )

  (defun main (MOD_HASH MERKLE_ROOT output_conditions)
    (if (should_recreate output_conditions)
      (c
        (list
          CREATE_COIN
          (curry_hashes_inline MOD_HASH (sha256 1 MOD_HASH) (sha256 1 MERKLE_ROOT))
          1 ; NFT1 amount is always 1
        )
        output_conditions
      )
      ; else
      output_conditions
    )
  )

  (if (= MERKLE_ROOT (simplify_merkle_proof (sha256tree puzzle_reveal) merkle_proof))
    (main MOD_HASH MERKLE_ROOT (a puzzle_reveal puzzle_solution))
    ; else
    (x "ph proof invalid")
  )
)
