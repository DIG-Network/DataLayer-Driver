; authorized_writer_filter.clsp
; for DataLayer.storage by yakuhito

;; Used as a filter that wraps an authorized writer puzzle.
;; Allows the data store owner to add authorized writers that:
;;  - CAN update the store root hash, label, description, and any other metadata
;;    (i.e., call the metadata updatter via the -24 condition)
;;  - CAN update oracle payout condition
;;    (i.e., use the -13 condition)
;;  - CANNOT update the authorized writers list
;;    (i.e., cannot create CREATE_COINs)
;;  - CANNOT emit NEW_OWNER_CONDITION for the ownership layer
;;    (i.e., cannot use the -10 condition)

(mod (
    INNER_PUZZLE
    inner_solution
  )

  (include condition_codes.clib)

  (defun morph_conditions (conditions)
    (if conditions
      (if (any
        (= (f (f conditions)) -10) ; -10 is NEW_OWNER_CONDITION for ownership layer
        (= (f (f conditions)) CREATE_COIN)
      )
        (x)
        ; else
        (c (f conditions) (morph_conditions (r conditions)))
      )
      ; else - all conditions checked
      ()
    )
  )

  (morph_conditions (a INNER_PUZZLE inner_solution))
)
