; writer_filter.clsp
; for DataLayer.storage by yakuhito

;; Allows the data store owner to add writers that:
;;  - CAN update the store root hash, label, description, and any other metadata
;;    (i.e., call the metadata updatter via the -24 condition)
;;  - CANNOT update the authorized puzzles list
;;    (i.e., cannot create CREATE_COINs)
;;
;; Warning: This filter only works for the default data layer metadata updater (11).

(mod (
    INNER_PUZZLE
    inner_solution
  )

  (include condition_codes.clib)

  (defun morph_conditions (conditions)
    (if conditions
      (if (any
        (= (f (f conditions)) CREATE_COIN)
        (= (f (f conditions)) -113) ; don't allow melting
        (= (f (f conditions)) -13) ; don't allow updating merkle root
        ; don't allow metadata updater to create extra conditions
        ; update metadata condition is (list -24 [metadata updater puzzle reveal] [solution])
        ; for the standard DL, solution is (list (list [new metadata] [new updater ph]) [conditions])
        ; return true if cond = -24 and [conditions] != () since true means we (x)
        (if (= (f (f conditions)) -24) (not (= (f (r (f (r (r (f conditions)))))) ())) ()) 
      )
        (x)
        ; else
        (c (f conditions) (morph_conditions (r conditions)))
      )
      ; else - all conditions checked
      ()
    )
  )

  (morph_conditions (a INNER_PUZZLE inner_solution))
)
