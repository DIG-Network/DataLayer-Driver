; admin_filter.clsp
; for DataLayer.storage by yakuhito

;; Allows the data store owner to add admins that:
;;  - CAN update the store root hash, label, description, and any other metadata
;;    (i.e., call the metadata updatter via the -24 condition)
;;  - CAN update the authorized puzzles list
;;    (i.e., can add other authorized writers and admins, alter oracle address/fees, etc. by using te -13 condition)
;;  - CANNOT remove the owner
;;    (i.e., output CREATE_COIN that could delete the one_of_many layer)
;;
;; Warning: This filter only works for the default data layer metadata updater (11).

(mod (
    INNER_PUZZLE
    inner_solution
  )

  (include condition_codes.clib)

  (defun morph_conditions (conditions)
    (if conditions
      (if (any
        (= (f (f conditions)) CREATE_COIN)
        (= (f (f conditions)) -113) ; don't allow melting
        (all
          (= (f (f conditions)) -24)
          ; condition is (list -24 [metadata updater puzzle reveal] [solution])
          ; for this puzzle, solution is (list (list [new metadata] [new updater ph]) [new conditions])
          (not (= (f (f (f (r (r (f conditions)))))) ()))
        ) ; don't allow metadata updater to create extra conditions
      )
        (x)
        ; else
        (c (f conditions) (morph_conditions (r conditions)))
      )
      ; else - all conditions checked
      ()
    )
  )

  (morph_conditions (a INNER_PUZZLE inner_solution))
)
