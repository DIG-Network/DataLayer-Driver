; admin_filter.clsp
; for DataLayer.storage by yakuhito

;; Allows the data store owner to add admins that:
;;  - CAN update the store root hash, label, description, and any other metadata
;;    (i.e., call the metadata updatter via the -24 condition)
;;  - CAN update the authorized puzzles list
;;    (i.e., can add other authorized writers and admins, alter oracle address/fees, etc. by using te -13 condition)
;;  - CANNOT remove the owner
;;    (i.e., output CREATE_COIN that could delete the one_of_many layer)
;;
;; Warning: This filter only works for the default data layer metadata updater (11).

(mod (
    INNER_PUZZLE
    ROOT_VALIDATOR_HASH
    inner_solution
    root_validator_reveal
  )

  (include condition_codes.clib)
  (include sha256tree.clib)

  (defun morph_conditions (ROOT_VALIDATOR_HASH root_validator_reveal conditions)
    (if conditions
      (if (any
        (= (f (f conditions)) CREATE_COIN)
        ; don't allow metadata updater to create extra conditions
        ; update metadata condition is (list -24 [metadata updater puzzle reveal] [solution])
        ; for the standard DL, solution is (list (list [new metadata] [new updater ph]) [conditions])
        ; return true if cond = -24 and [conditions] != () or new updater ph != current updater ph since true means we (x)
        (if (= (f (f conditions)) -24)
          (not (all
            (= (f (r (f (r (r (f conditions)))))) ())
            (= (f (r (f (f (r (r (f conditions))))))) (sha256 1 11))
          ))
          ()
        )
        (if (= (f (f conditions)) -13)
          (not (all
            (= (sha256tree root_validator_reveal) ROOT_VALIDATOR_HASH)
            (= (f (r (f conditions))) (a root_validator_reveal (list (r (r (f conditions))))))
          ))
          ()
        )
      )
        (x)
        ; else
        (c (f conditions) (morph_conditions ROOT_VALIDATOR_HASH root_validator_reveal (r conditions)))
      )
      ; else - all conditions checked
      ()
    )
  )

  (morph_conditions ROOT_VALIDATOR_HASH root_validator_reveal (a INNER_PUZZLE inner_solution))
)
