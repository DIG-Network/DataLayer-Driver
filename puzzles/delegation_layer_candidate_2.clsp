; delegation_layer.clsp
; for DataLayer.storage by yakuhito

;; Allows the owner of a store to delegate a fixed set of capabilities to
;; other puzzle hashes.
;; This layer will only re-create itself if the inner puzzle did not emit any
;; odd-amount CREATE_COIN condition. 
;; Capabilities are restricted via filters.

(mod (
    MOD_HASH
    INNER_PUZZLE_HASH
    AUTHORIZED_PUZZLES
    puzzle_reveal
    puzzle_solution
  )

  (include condition_codes.clib)
  (include sha256tree.clib)
  (include curry.clib)

  (defconstant NEW_AUTHORIZED_PUZZLES_CONDITION -13)

  ; only one odd CREATE_COIN enforced in upper layers
  (defun morph_conditions (MOD_HASH INNER_PUZZLE_HASH conditions new_authorized_puzzles_info odd_create_coin_found)
    (if conditions
      (if (all (= (f (f conditions)) NEW_AUTHORIZED_PUZZLES_CONDITION) (= (strlen (f (r (f conditions)))) 32))
        (morph_conditions
          MOD_HASH INNER_PUZZLE_HASH
          (r conditions)
          (r (f conditions))
          odd_create_coin_found
        )
        ; else
        (c
          (f conditions)
          (morph_conditions
            MOD_HASH INNER_PUZZLE_HASH
            (r conditions)
            new_authorized_puzzles_info
            (any
              odd_create_coin_found
              (all (= (f (f conditions)) CREATE_COIN) (= (logand (f (r (r (f conditions)))) 1) 1))
            )
          )
        )
      )
      ; else
      (if odd_create_coin_found
        ()
        ; else
        (list
          (list
            CREATE_COIN
            (curry_hashes_inline MOD_HASH
              (sha256 1 MOD_HASH)
              (sha256 1 INNER_PUZZLE_HASH)
              (sha256tree (f new_authorized_puzzles_info))
            )
            1
            (r new_authorized_puzzles_info)
          )
        )
      )
    )
  )

  (defun item_in_list (item list)
    (if list
      (if (= item (f list))
        1
        ; else
        (item_in_list item (r list))
      )
      ; else
      ()
    )
  )

  (defun-inline main (
    puzzle_hash
  )
    (if (= puzzle_hash INNER_PUZZLE_HASH)
      (a puzzle_reveal puzzle_solution)
      ; else
      (if (item_in_list puzzle_hash AUTHORIZED_PUZZLES)
        (morph_conditions MOD_HASH INNER_PUZZLE_HASH (a puzzle_reveal puzzle_solution) (list MERKLE_ROOT) ())
        ; else
        (x "ph proof invalid")
      )
    )
  )

  (main (sha256tree puzzle_reveal))
)
